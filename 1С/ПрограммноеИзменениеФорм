```bsl
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ПриСозданииНаСервере_ГенерацияЭлементовФормы(Форма);		
	
КонецПроцедуры

#Область ГенерацияЭлементовФормы_Интерфейс

Процедура ПриСозданииНаСервере_ГенерацияЭлементовФормы(Форма)
	
	ЧастиИмениФормыРазделенныеТочками = СтрРазделить(Форма.ИмяФормы, ".");
	ТипОбъекта = ЧастиИмениФормыРазделенныеТочками[1];
	ИмяФормы   = ЧастиИмениФормыРазделенныеТочками[3]; 
	
	Если ТипОбъекта = "Номенклатура" Тогда
		ГенерацияЭлементовФормы_Номенклатура(Форма, ИмяФормы);		
	КонецЕсли;

КонецПроцедуры

Процедура ГенерацияЭлементовФормы_Номенклатура(Форма,  ИмяФормы)
	
	Если ИмяФормы = "ФормаЭлемента" Тогда
		ДобавитьНовыеРеквизитыВКарточкуНоменклатуры(Форма);		
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ГенерацияЭлементовФормы_Реализация

Процедура ДобавитьНовыеРеквизитыВКарточкуНоменклатуры(Форма)
	
	ИменаКопируемыхЭлементов = Новый ДеревоЗначений;
	ИменаКопируемыхЭлементов.Колонки.Добавить("Имя");
	
	ГруппаЗаголовок = ИменаКопируемыхЭлементов.Строки.Добавить();
	ГруппаЗаголовок.Имя = "дк_ГруппаЗаголовокДобавленныеРеквизиты";
	
	//Вместо префикса постфикс, так как типовой механизм обрезает строку через СтрЗаменить (см. СвернутьРазвернутьГруппу в форме элемента номенклатуры)
	
		ДекорацияОткрытьДобавленныеРеквизиты = ГруппаЗаголовок.Строки.Добавить();
		ДекорацияОткрытьДобавленныеРеквизиты.Имя = "ДекорацияОткрытьДобавленныеРеквизиты_дк";
		
		ДекорацияОткрытьДобавленныеРеквизиты = ГруппаЗаголовок.Строки.Добавить();
		ДекорацияОткрытьДобавленныеРеквизиты.Имя = "ДекорацияЗакрытьДобавленныеРеквизиты_дк";
		
		ДекорацияОткрытьДобавленныеРеквизиты = ГруппаЗаголовок.Строки.Добавить();
		ДекорацияОткрытьДобавленныеРеквизиты.Имя = "ЗаголовокГруппыДобавленныеРеквизиты_дк";
	
	Элемент_ГруппаЗаголовокДобавленныеРеквизиты = СоздатьКопиюЭлемента(Форма, Форма.Элементы.ГруппаЗаголовокЕдиницыИзмерения, , Форма.Элементы.ГруппаПраво, ИменаКопируемыхЭлементов);
	Элемент_ГруппаЗаголовокДобавленныеРеквизиты.Заголовок = "Добавленные реквизиты";
	Элемент_ДекорацияЗаголовка = Элемент_ГруппаЗаголовокДобавленныеРеквизиты.ПодчиненныеЭлементы.ЗаголовокГруппыДобавленныеРеквизиты_дк;
	Элемент_ДекорацияЗаголовка.Заголовок = "Добавленные реквизиты";
	

	ИменаКопируемыхЭлементов = Новый ДеревоЗначений;
	ИменаКопируемыхЭлементов.Колонки.Добавить("Имя");
	
	СворачиваемаяГруппаДобавленныеРеквизиты = ИменаКопируемыхЭлементов.Строки.Добавить();
	СворачиваемаяГруппаДобавленныеРеквизиты.Имя = "СворачиваемаяГруппаДобавленныеРеквизиты_дк";

	Элемент_СворачиваемаяГруппаДобавленныеРеквизиты = СоздатьКопиюЭлемента(Форма, Форма.Элементы.СворачиваемаяГруппаЕдиницыИзмерения, , Форма.Элементы.ГруппаПраво, ИменаКопируемыхЭлементов, Ложь);

	ДобавитьПолеФормы(
		Форма,
		"дк_ОграничениеСкидки",
		Элемент_СворачиваемаяГруппаДобавленныеРеквизиты,
		"Объект.дк_ОграничениеСкидки",
		"Ограничение скидки",
		ВидПоляФормы.ПолеВвода);
		
	ДобавитьПолеФормы(
		Форма, 
		"дк_КлассификаторСайта",
		Элемент_СворачиваемаяГруппаДобавленныеРеквизиты,
		"Объект.дк_КлассификаторСайта",
		"Классификатор сайта",
		ВидПоляФормы.ПолеВвода);

	ДобавитьПолеФормы(
		Форма, 
		"дк_ВыводитсяИзАссортимента",
		Элемент_СворачиваемаяГруппаДобавленныеРеквизиты,
		"Объект.дк_ВыводитсяИзАссортимента",
		"Выводится из ассортимента",
		 ВидПоляФормы.ПолеФлажка);

	ДобавитьПолеФормы(
		Форма, 
		"дк_Новинка",
		Элемент_СворачиваемаяГруппаДобавленныеРеквизиты,
		"Объект.дк_Новинка",
		"Новинка",
		ВидПоляФормы.ПолеФлажка);

КонецПроцедуры

#КонецОбласти

#Область ГенерацияЭлементовФормы_ОбщегоНазначения

Функция ДобавитьПолеФормы(Форма, ИмяЭлемента, Группа, ПутьКДанным, Заголовок, ВидПоля = Неопределено, Эталон = Неопределено)
	
	НовыйЭлемент = Форма.Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), Группа);

	Если Эталон <> Неопределено Тогда     
		ИсключаемыеСвойства = "ПутьКДанным, Заголовок"; 
		Если Эталон.Вид = ВидПоляФормы.ПолеВвода Тогда
			ИсключаемыеСвойства = ИсключаемыеСвойства + ", ВыделенныйТекст, СвязьПоТипу";
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Эталон, , ИсключаемыеСвойства);
	ИначеЕсли ВидПоля <> Неопределено Тогда
		НовыйЭлемент.Вид = ВидПоля;
	КонецЕсли;

	НовыйЭлемент.ПутьКДанным = ПутьКДанным;
	НовыйЭлемент.Заголовок = Заголовок;

	Возврат НовыйЭлемент; 

КонецФункции
```
