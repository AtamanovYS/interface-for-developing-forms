```bsl
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ЧастиИмениФормыРазделенныеТочками = СтрРазделить(Форма.ИмяФормы, ".");
	ТипОбъекта = ЧастиИмениФормыРазделенныеТочками[1];
	ИмяФормы   = ЧастиИмениФормыРазделенныеТочками[3]; 
	
	Если ТипОбъекта = "ЗаказКлиента" Тогда
		Если ИмяФормы = "ФормаДокумента" Тогда

		КонецЕсли;
	ИначеЕсли ТипОбъекта = "Номенклатура" Тогда	
		Если ИмяФормы = "ФормаЭлемента" Тогда
		
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#Область ГенерацияЭлементовФормы_СозданиеЭлементов

Функция ДобавитьПолеФормы(Форма, Поле, ВидПоля, Группа = Неопределено, ПутьКДанным = Неопределено, Эталон = Неопределено, ЭлементПередКоторымВставить = Неопределено)
	
	Родитель = Поле.Родитель;
	Поле = Поле.Реквизит;
	ИмяЭлемента = ?(Родитель = Неопределено, Поле.Имя, Родитель.Имя + Поле.Имя);
	
	Если ЭлементПередКоторымВставить = Неопределено Тогда
		Если Группа = Неопределено Тогда
			НовыйЭлемент = Форма.Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"));
		Иначе
			НовыйЭлемент = Форма.Элементы.Добавить(ИмяЭлемента, Тип("ПолеФормы"), Группа);
		КонецЕсли;
	Иначе
		Если Группа = Неопределено Тогда
			НовыйЭлемент = Форма.Элементы.Вставить(ИмяЭлемента, Тип("ПолеФормы"), , ЭлементПередКоторымВставить);
		Иначе
			НовыйЭлемент = Форма.Элементы.Вставить(ИмяЭлемента, Тип("ПолеФормы"), Группа, ЭлементПередКоторымВставить);
		КонецЕсли;
	КонецЕсли;

	Если Эталон <> Неопределено Тогда     
		ИсключаемыеСвойства = "ПутьКДанным, Заголовок"; 
		Если Эталон.Вид = ВидПоляФормы.ПолеВвода Тогда
			ИсключаемыеСвойства = ИсключаемыеСвойства + ", ВыделенныйТекст, СвязьПоТипу";
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Эталон, , ИсключаемыеСвойства);
	ИначеЕсли ВидПоля <> Неопределено Тогда
		НовыйЭлемент.Вид = ВидПоля;	
	КонецЕсли;

	Попытка
		НовыйЭлемент.Заголовок = Поле.Заголовок;
		ЭтоРеквизитОбъекта = Ложь;
	Исключение
		НовыйЭлемент.Заголовок = Поле.Синоним;
		ЭтоРеквизитОбъекта = Истина;
	КонецПопытки;
	
	НовыйЭлемент.ПутьКДанным = ?(ПутьКДанным = Неопределено, ?(ЭтоРеквизитОбъекта, "Объект." + ?(Родитель = Неопределено, Поле.Имя, Родитель.Имя + "." + Поле.Имя), Поле.Имя), ПутьКДанным);

	Возврат НовыйЭлемент; 

КонецФункции

Функция ДобавитьКнопкуФормы(Форма, Команда, ВидКоманды = Неопределено, Группа = Неопределено, Эталон = Неопределено, ЭлементПередКоторымВставить = Неопределено)
	
	Если ЭлементПередКоторымВставить = Неопределено Тогда
		Если Группа = Неопределено Тогда
			НовыйЭлемент = Форма.Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"));
		Иначе
			НовыйЭлемент = Форма.Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Группа);
		КонецЕсли;
	Иначе
		Если Группа = Неопределено Тогда
			НовыйЭлемент = Форма.Элементы.Вставить(Команда.Имя, Тип("КнопкаФормы"), , ЭлементПередКоторымВставить);
		Иначе
			НовыйЭлемент = Форма.Элементы.Вставить(Команда.Имя, Тип("КнопкаФормы"), Группа, ЭлементПередКоторымВставить);
		КонецЕсли;
	КонецЕсли;

	Если Эталон <> Неопределено Тогда     
		ИсключаемыеСвойства = "ИмяКоманды, Заголовок"; 
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Эталон, , ИсключаемыеСвойства);
	ИначеЕсли ВидКоманды <> Неопределено Тогда
		НовыйЭлемент.Вид = ВидКоманды;
	КонецЕсли;

	НовыйЭлемент.ИмяКоманды = Команда.Имя;
	НовыйЭлемент.Заголовок = Команда.Заголовок;

	Возврат НовыйЭлемент; 

КонецФункции

Функция ПолучитьРеквизит(Форма, Путь)
	СтруктураВозврата = Новый Структура("Реквизит, Родитель");
	ПутьРазделенныйТочками = СтрРазделить(Путь, ".");
	Если ПутьРазделенныйТочками.Количество() > 1 И ПутьРазделенныйТочками[0] = "Объект" Тогда
		ПутьРазделенныйТочками.Удалить(0);
		Если ПутьРазделенныйТочками.Количество() = 1 Тогда
			СтруктураВозврата.Реквизит = Форма.РеквизитФормыВЗначение("Объект").Метаданные().Реквизиты[ПутьРазделенныйТочками[0]];
			Возврат СтруктураВозврата;
		Иначе
			СтруктураВозврата.Родитель = Форма.РеквизитФормыВЗначение("Объект").Метаданные().ТабличныеЧасти[ПутьРазделенныйТочками[0]];
			СтруктураВозврата.Реквизит = СтруктураВозврата.Родитель.Реквизиты[ПутьРазделенныйТочками[1]]; 
			Возврат СтруктураВозврата;	
		КонецЕсли 	
	КонецЕсли;
	МассивРеквизитов = Форма.ПолучитьРеквизиты();
	Для Каждого Стр Из МассивРеквизитов Цикл
		Если Стр.Имя = Путь Тогда
			СтруктураВозврата.Реквизит = Стр;
			Возврат СтруктураВозврата;
		КонецЕсли;
	КонецЦикла;
	Возврат Неопределено;
КонецФункции

#КонецОбласти


```
