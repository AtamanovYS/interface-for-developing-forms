```bsl
Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	ЧастиИмениФормыРазделенныеТочками = СтрРазделить(Форма.ИмяФормы, ".");
	ТипОбъекта = ЧастиИмениФормыРазделенныеТочками[1];
	ИмяФормы   = ЧастиИмениФормыРазделенныеТочками[3]; 
	
	Если ТипОбъекта = "ЗаказКлиента" Тогда
		Если ИмяФормы = "ФормаДокумента" Тогда
			ДобавитьКомандуПодготовкиДокументовПеремещенияТоваров(Форма);	
		КонецЕсли;
	ИначеЕсли ТипОбъекта = "РеализацияТоваровУслуг" Тогда	
		Если ИмяФормы = "ФормаДокумента" Тогда
			ДобавитьПоле(Форма);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


#Область ПриСозданииНаСервере_ЗаказКлиента

Процедура ДобавитьКомандуПодготовкиДокументовПеремещенияТоваров(Форма)

	Команда = Форма.Команды.Добавить("дк_ПодготовитьДокументПеремещенияТоваров");
	Команда.Заголовок = "Подготовить документ перемещения товаров";
	Команда.Действие = "Подключаемый_ВыполнитьПереопределяемуюКоманду";
	Команда.Картинка = БиблиотекаКартинок.ОформитьПоРаспоряжению;
	
	Кнопка = ДобавитьКнопкуФормы(Форма, Команда, Форма.Элементы.ГруппаСтатусПриоритет, ВидКнопкиФормы.Гиперссылка, , Форма.Элементы.ГруппаСостояние);
	Кнопка.Заголовок = "Перемещение";
	
КонецПроцедуры

#КонецОбласти

#Область ГенерацияЭлементовФормы_ОбщегоНазначения

Функция ДобавитьПолеФормы(Форма, Поле, Группа, ВидПоля = Неопределено, ПутьКДанным = Неопределено, Эталон = Неопределено, ЭлементПередКоторымВставить = Неопределено)
	
	Если ЭлементПередКоторымВставить = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(Поле.Имя, Тип("ПолеФормы"), Группа);
	Иначе
		НовыйЭлемент = Форма.Элементы.Вставить(Поле.Имя, Тип("ПолеФормы"), Группа, ЭлементПередКоторымВставить);
	КонецЕсли;

	Если Эталон <> Неопределено Тогда     
		ИсключаемыеСвойства = "ПутьКДанным, Заголовок"; 
		Если Эталон.Вид = ВидПоляФормы.ПолеВвода Тогда
			ИсключаемыеСвойства = ИсключаемыеСвойства + ", ВыделенныйТекст, СвязьПоТипу";
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Эталон, , ИсключаемыеСвойства);
	ИначеЕсли ВидПоля <> Неопределено Тогда
		НовыйЭлемент.Вид = ВидПоля;
	КонецЕсли;

	НовыйЭлемент.ПутьКДанным = ?(ПутьКДанным = Неопределено, Поле.Имя, ПутьКДанным);
	НовыйЭлемент.Заголовок = Поле.Заголовок;

	Возврат НовыйЭлемент; 

КонецФункции

Функция ДобавитьКнопкуФормы(Форма, Команда, Группа, ВидКоманды = Неопределено, Эталон = Неопределено, ЭлементПередКоторымВставить = Неопределено)
	
	Если ЭлементПередКоторымВставить = Неопределено Тогда
		НовыйЭлемент = Форма.Элементы.Добавить(Команда.Имя, Тип("КнопкаФормы"), Группа);
	Иначе
		НовыйЭлемент = Форма.Элементы.Вставить(Команда.Имя, Тип("КнопкаФормы"), Группа, ЭлементПередКоторымВставить);
	КонецЕсли;	

	Если Эталон <> Неопределено Тогда     
		ИсключаемыеСвойства = "ИмяКоманды, Заголовок"; 
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, Эталон, , ИсключаемыеСвойства);
	ИначеЕсли ВидКоманды <> Неопределено Тогда
		НовыйЭлемент.Вид = ВидКоманды;
	КонецЕсли;

	НовыйЭлемент.ИмяКоманды = Команда.Имя;
	НовыйЭлемент.Заголовок = Команда.Заголовок;

	Возврат НовыйЭлемент; 

КонецФункции

#КонецОбласти
```
