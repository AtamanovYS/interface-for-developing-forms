Функция ПолучитьЗначениеПоИмениГруппировкиВРасшифровке(ИмяГруппировки, ДанныеРасшифровки, ТекущаяРасшифровка)
	
	ДанныеРасшифровкиИзХранилища = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	ПоляРасшифровки = ПолучитьПоляРасшифровкиВВидеТаблицы(ТекущаяРасшифровка, ДанныеРасшифровкиИзХранилища);
	НайденнаяСтрока = ПоляРасшифровки.Найти(ИмяГруппировки, "Поле");
	Если Не НайденнаяСтрока = Неопределено Тогда
		Возврат НайденнаяСтрока.Значение;
	КонецЕсли;
КонецФункции

Функция ПолучитьПоляРасшифровкиВВидеТаблицы(ТекущаяРасшифровка, ДанныеРасшифровки)
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("Уровень"); 
	Результат.Колонки.Добавить("Поле"); 
	Результат.Колонки.Добавить("Значение");
	Результат.Колонки.Добавить("Идентификатор"); 

	ДобавитьПолеРасшифровкиВТаблицуРекурсивно(Результат, ТекущаяРасшифровка, ДанныеРасшифровки);
	
	Возврат Результат;
КонецФункции

Процедура ДобавитьПолеРасшифровкиВТаблицуРекурсивно(ТаблицаРасшифровки, ТекущаяРасшифровка, ДанныеРасшифровки, Уровень = 0)
	
	ЭлементРасшифровки = ДанныеРасшифровки.Элементы[ТекущаяРасшифровка];
	
	УровеньРодителя = Уровень;
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		УровеньРодителя = УровеньРодителя + 1;
		
		МассивПолей = ЭлементРасшифровки.ПолучитьПоля();
		Для Каждого Поле Из МассивПолей Цикл
			НоваяСтрока = ТаблицаРасшифровки.Добавить();
			НоваяСтрока.Поле = Поле.Поле;
			НоваяСтрока.Значение = Поле.Значение;
			НоваяСтрока.Уровень = Уровень;
			НоваяСтрока.Идентификатор = ЭлементРасшифровки.Идентификатор;
			
		КонецЦикла;
	КонецЕсли;
	
	МассивРодителей = ЭлементРасшифровки.ПолучитьРодителей();
	Для Каждого Родитель Из МассивРодителей Цикл
		ДобавитьПолеРасшифровкиВТаблицуРекурсивно(
				ТаблицаРасшифровки, 
				Родитель.Идентификатор,
				ДанныеРасшифровки,
				УровеньРодителя);
	КонецЦикла;
	
КонецПроцедуры

Функция ПолучитьЗначениеПоляРасшифровки(Расшифровка)
	ДанныеОтчета = ПолучитьИзВременногоХранилища(ОтчетДанныеРасшифровки);
	Поля = ДанныеОтчета.Элементы.Получить(Расшифровка).ПолучитьПоля();
	Если Поля.Количество() > 1 Тогда
		МассивЗначений = Новый Массив;
		Для Каждого Поле Из Поля Цикл
			МассивЗначений.Добавить(Поле.Значение);	
		КонецЦикла;
		Возврат МассивЗначений;
	Иначе
    	Возврат ДанныеОтчета.Элементы.Получить(Расшифровка).ПолучитьПоля()[0].Значение;	
	КонецЕсли;
КонецФункции

Функция ПолучитьПолеРасшифровки(Расшифровка)
	ДанныеОтчета = ПолучитьИзВременногоХранилища(ОтчетДанныеРасшифровки);
	Поля = ДанныеОтчета.Элементы.Получить(Расшифровка).ПолучитьПоля();
    Возврат ДанныеОтчета.Элементы.Получить(Расшифровка).ПолучитьПоля()[0].Поле;	
КонецФункции
