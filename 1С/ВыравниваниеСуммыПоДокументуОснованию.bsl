// Код разрабатывался в обычном приложении

// Процедура необходима, когда нужно выравнять суммы в документе-основании и в создаваемом документе
// Например, при установке скидок в реализации общая сумма документа может измениться в сравнении с заказом
// 
// Параметры:
//  ТабличнаяЧастьОснования - табличная часть документа-основания
//  ТабличнаяЧасть - табличная часть создаваемого документа
//
Процедура ВыравнятьСуммуПоДокументуОснованию(ТабличнаяЧастьОснования, ТабличнаяЧасть) Экспорт
	
	ТЗДокумента = ТабличнаяЧасть.Выгрузить();
	ТЗОснования = ТабличнаяЧастьОснования.Выгрузить();

	ЕстьКолонкаСумма = ТЗДокумента.Колонки.Найти("Сумма") <> Неопределено И ТЗОснования.Колонки.Найти("Сумма") <> Неопределено;
	ЕстьКолонкаСуммаНДС = ТЗДокумента.Колонки.Найти("суммаНДС") <> Неопределено И ТЗОснования.Колонки.Найти("суммаНДС") <> Неопределено;
	ЕстьКолонкаКоличество = ТЗДокумента.Колонки.Найти("Количество") <> Неопределено И ТЗОснования.Колонки.Найти("Количество") <> Неопределено;
	
	Если НЕ ЕстьКолонкаКоличество Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВОснованииИВДокументеОдинаковыеПозиции(ТЗДокумента, ТЗОснования) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьКолонкаСумма Тогда 	
		СуммаОбщаяОснования = ТабличнаяЧастьОснования.Итог("сумма");
		СуммаОбщая = ТабличнаяЧасть.Итог("Сумма");
	КонецЕсли;
	
	Если ЕстьКолонкаСуммаНДС Тогда 
		СуммаОбщаяНДСОснования = ТабличнаяЧастьОснования.Итог("суммаНДС");
		СуммаОбщаяНДС = ТабличнаяЧасть.Итог("суммаНДС");
	КонецЕсли;
	
	КоличествоОбщее = ТабличнаяЧасть.Итог("Количество");
	КоличествоОбщееОснование = ТабличнаяЧастьОснования.Итог("Количество");
	
	Если КоличествоОбщее <> КоличествоОбщееОснование Тогда
		ВызватьИсключение "В документе основании и в созданном документе не сходится количество, в основании " + Строка(КоличествоОбщееОснование) + ", а в заказе " + Строка(КоличествоОбщее);
	Иначе	
		Если ЕстьКолонкаСумма Тогда 
			Если СуммаОбщая <> СуммаОбщаяОснования Тогда
				Погрешность = СуммаОбщая - СуммаОбщаяОснования;
				МассивКоэфф = ТабличнаяЧасть.ВыгрузитьКолонку("Сумма");
				МассивСумм = ОбщегоНазначения.РаспределитьПропорционально(Погрешность, МассивКоэфф);
				Индекс = 0;
				Для Каждого Стр Из ТабличнаяЧасть Цикл
					Стр.Сумма = Стр.Сумма - МассивСумм[Индекс];
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьКолонкаСуммаНДС Тогда
			Если СуммаОбщаяНДС <> СуммаОбщаяНДСОснования Тогда
				Погрешность = СуммаОбщаяНДС - СуммаОбщаяНДСОснования;
				МассивКоэфф = ТабличнаяЧасть.ВыгрузитьКолонку("СуммаНДС");
				МассивСумм = ОбщегоНазначения.РаспределитьПропорционально(Погрешность, МассивКоэфф);
				Индекс = 0;
				Для Каждого Стр Из ТабличнаяЧасть Цикл
					Стр.СуммаНДС = Стр.СуммаНДС - МассивСумм[Индекс];
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

Функция ВОснованииИВДокументеОдинаковыеПозиции(Знач ТЗДокумента, Знач ТЗОснования)
	ТЗДокумента.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения", "Количество");
	ТЗОснования.Свернуть("Номенклатура, ХарактеристикаНоменклатуры, ЕдиницаИзмерения", "Количество");
	
	ДельтаСтрокПервойТаблицы = Новый Массив;
	ДельтаСтрокВторойТаблицы = Новый Массив;

	// см. "Быстрое сравнение коллекций с выводом разницы"
	НайтиОтличияТаблиц(ТЗОснования, ТЗДокумента, ДельтаСтрокПервойТаблицы, ДельтаСтрокВторойТаблицы);	
	
	Возврат ДельтаСтрокПервойТаблицы.Количество() = 0 И ДельтаСтрокВторойТаблицы.Количество() = 0;  

КонецФункции
