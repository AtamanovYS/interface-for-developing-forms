```bsl
Процедура РаспределитьПогрешностьПриЕеНаличии(Основание, Товары) Экспорт
	ТЗДокумента = Товары.Выгрузить();
	Попытка
		ТЗОснования = Основание.Товары.Выгрузить();
	Исключение
		Возврат;
	КонецПопытки;
	ЕстьКолонкаСумма = ТЗДокумента.Колонки.Найти("Сумма") <> Неопределено И ТЗОснования.Колонки.Найти("Сумма") <> Неопределено;
	ЕстьКолонкаСуммаНДС = ТЗДокумента.Колонки.Найти("суммаНДС") <> Неопределено И ТЗОснования.Колонки.Найти("суммаНДС") <> Неопределено;
	ЕстьКолонкаКоличество = ТЗДокумента.Колонки.Найти("Количество") <> Неопределено И ТЗОснования.Колонки.Найти("Количество") <> Неопределено;
	
	Если НЕ ЕстьКолонкаКоличество Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ВОснованииИВДокументеОдинаковыеПозиции(ТЗДокумента, ТЗОснования) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьКолонкаСумма Тогда 	
		СуммаОбщаяОснования = Основание.Товары.Итог("сумма");
		СуммаОбщая = Товары.Итог("Сумма");
	КонецЕсли;
	
	Если ЕстьКолонкаСуммаНДС Тогда 
		СуммаОбщаяНДСОснования = Основание.Товары.Итог("суммаНДС");
		СуммаОбщаяНДС = Товары.Итог("суммаНДС");
	КонецЕсли;
	
	КоличествоОбщее = Товары.Итог("Количество");
	КоличествоОбщееОснование = Основание.Товары.Итог("Количество");
	
	Если КоличествоОбщее <> КоличествоОбщееОснование Тогда
		Сообщить("Что-то не сходится, количество в заказе и реалке разное получилось", СтатусСообщения.ОченьВажное);
		ЗаписьОшибки = РегистрыСведений.дк_ОшибкиИДопИнформация.СоздатьМенеджерЗаписи();
		ЗаписьОшибки.Текст = "При создании реализации из заказа количество не сошлось, в основании " + Строка(КоличествоОбщееОснование) + ", а в заказе " + Строка(КоличествоОбщее);
		ЗаписьОшибки.Документ = Основание;
		ЗаписьОшибки.Период = ТекущаяДата();
		ЗаписьОшибки.ЭтоОшибка = Истина;
		ЗаписьОшибки.Записать();
	Иначе	
		Если ЕстьКолонкаСумма Тогда 
			Если СуммаОбщая <> СуммаОбщаяОснования Тогда
				Погрешность = СуммаОбщая - СуммаОбщаяОснования;
				МассивКоэфф = Товары.ВыгрузитьКолонку("Сумма");
				МассивСумм = ОбщегоНазначения.РаспределитьПропорционально(Погрешность, МассивКоэфф);
				Индекс = 0;
				Для Каждого Стр Из Товары Цикл
					Стр.Сумма = Стр.Сумма - МассивСумм[Индекс];
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ЕстьКолонкаСуммаНДС Тогда
			Если СуммаОбщаяНДС <> СуммаОбщаяНДСОснования Тогда
				Погрешность = СуммаОбщаяНДС - СуммаОбщаяНДСОснования;
				МассивКоэфф = Товары.ВыгрузитьКолонку("СуммаНДС");
				МассивСумм = ОбщегоНазначения.РаспределитьПропорционально(Погрешность, МассивКоэфф);
				Индекс = 0;
				Для Каждого Стр Из Товары Цикл
					Стр.СуммаНДС = Стр.СуммаНДС - МассивСумм[Индекс];
					Индекс = Индекс + 1;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры
```
